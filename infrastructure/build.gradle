buildscript {
    dependencies {
        classpath('org.flywaydb:flyway-mysql:9.19.4')
    }
}

plugins {
    id 'application'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.flywaydb.flyway' version '9.19.4'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':domain')
    implementation project(':application')

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:5.3.1'
    testImplementation 'org.flywaydb:flyway-core:9.19.4'
    testImplementation 'org.flywaydb:flyway-mysql:9.19.4'
    testImplementation 'org.testcontainers:testcontainers:1.18.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.18.3'
    testImplementation 'org.testcontainers:mysql:1.18.3'
}

flyway {
    url = System.getenv('FLYWAY_MYSQL_URL') ?: 'jdbc:mysql://localhost:3306/tasks'
    user = System.getenv('FLYWAY_MYSQL_USERNAME') ?: 'root'
    password = System.getenv('FLYWAY_MYSQL_PASSWORD') ?: 'root123'
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = false
    }
}

jar {
    enabled = false
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
    group = 'verification'
    useJUnitPlatform {
        includeTags 'integrationTest'
    }
}

tasks.register('e2eTest', Test) {
    group = 'verification'
    useJUnitPlatform {
        includeTags 'e2eTest'
    }
}
